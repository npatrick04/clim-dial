;;; Copyright 2017, Nicholas Patrick
;;;
;;; This program is free software: you can redistribute it and/or modify
;;; it under the terms of the GNU Lesser General Public License as published by
;;; the Free Software Foundation, either version 3 of the License, or
;;; (at your option) any later version.
;;; 
;;; This program is distributed in the hope that it will be useful,
;;; but WITHOUT ANY WARRANTY; without even the implied warranty of
;;; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
;;; GNU Lesser General Public License for more details.
;;; 
;;; You should have received a copy of the GNU Lesser General Public License
;;; along with this program.  If not, see <http://www.gnu.org/licenses/>.

(in-package :test-clim-dial)

(define-application-frame clock-test ()
  ((done :accessor clock-test-done
	 :initform nil)
   (updater :accessor clock-test-updater))
  (:panes (clock (make-pane 'clock-pane
			    :name 'clock
			    :diameter 200))
	  (digital (make-pane 'text-field
			      :editable nil
			      :max-width 200)))
  (:layouts (default
		(vertically ()
		  clock
		  digital))))

;;; This is what creates the update tick for the clock.
(defmethod initialize-instance :after ((frame clock-test) &key &allow-other-keys)
  (setf (clock-test-updater frame)
	(bt:make-thread
	 (lambda ()
	   ;; TODO: use a semaphore to note when it's ready
	   (sleep 0.5)
	   (let ((clock (find-pane-named frame 'clock))
		 (digital (find-pane-named frame 'digital))
		 last-time)
	     (loop
		(when (clock-test-done frame)
		  (return nil))
		(let ((time (multiple-value-list
			     (get-decoded-time))))
		  ;; Only update the clock face when the time ticks
		  (when (not (equalp time last-time))
		    (setf last-time time)
		    (setf (gadget-value clock) time)
		    (setf (gadget-value digital)
			  (format nil "铂О暮铂О暮铂О宁蝈鲥蝮篚怏羼糸礤畅┅聃艴瀛蝈疳轭沆镢磲脲轭篝犷沐鏖钿秣蝈疳轭舡弼孱后桢弭沆镢候彗轱箬邋舡蝈玳镱沆镢氅┅┅换澡轶箪邋轶箬矧箫翳狒轸轶蝈箴镱箝鲥麸翳聃轸泔眄犷洚箪邋爱暴┅┅┅ㄤ彐礤翳镤骝犴瀛屮轸衡彐矧è骝犴沆镢氕翦篝┅箦翩ㄣ祜汶翦篝滹铄骝犴濠舂麒孱ㄡ钿ㄣ祜汶翦篝躔溽翦骝犴濠ㄢ艉翳蝈徜ㄣ祜汶翦篝躔溽翦骝犴濠ㄢ艉翳蝈徜犰轹瀛ㄣ祜汶翦篝躔溽翦骝犴濠┅ㄢ艉觑轭翳蝈徜ㄣ祜汶翦篝躔溽翦骝犴濠┅ㄤ彐躅翦篝沆镢é镳糸镱犰磲脲翳蝈徜⒁躅翳沆镢狃瓞麒孱土伺匀遗聊轶紊态蝓轭翳沲蝌孱翳蝈徜雉桢蝼轶蝓轸轭篚猸翳蝈徜蝈趱蝾轭翳骝犴瀹戾è骝犴磲脲狃痨殂狒轱瞽骝犴с祜汶翦篝┅ㄩ磲脲翳蝈徜痱镧ㄢ艉磲脲翳蝈徜灬礅溽ī蝓瞽骝犴瀛麸瓠戾鲥骝犴濠┅骝犴濠蝓瞽骝犴瀛麸瓠戾鲥骝犴濠┅